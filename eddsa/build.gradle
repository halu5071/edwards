apply plugin: 'java-library'
apply plugin: 'cpp'
apply plugin: 'google-test'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'io.moatwel.crypto'
archivesBaseName = 'eddsa'
version = '0.2.0-alpha'
description = 'Edwards is a crypto library for Edwards-curve Digital Signature Algorithm (EdDSA) written in pure Java. '

def powermock_version = '1.7.4'
def mockito_version = '2.8.9'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Apache Common Codec
    compile 'commons-codec:commons-codec:1.10'
    // SpongyCastle
    compile 'com.madgag.spongycastle:core:1.58.0.0'
    compile 'com.madgag.spongycastle:prov:1.58.0.0'
    // Test
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "org.mockito:mockito-inline:$mockito_version"
    testCompile "org.powermock:powermock-core:$powermock_version"
    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "org.powermock:powermock-api-mockito2:$powermock_version"
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir new File(project.parent.projectDir, "3rdparty/googletest-release/googletest/include")
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file(project.parent.projectDir.getPath() + "/3rdparty/googletest-release/googletest/build/" + findGoogleTestStaticLib(targetPlatform))
                }
            }
        }
    }
    components {
        point(NativeLibrarySpec) { }
    }
    binaries {
        withType(SharedLibraryBinarySpec) {
            if (toolChain in VisualCpp) {
                cppCompiler.define "DLL_EXPORT"
            }

            if (targetPlatform.operatingSystem.isMacOsX()) {
                def javaHome = System.getenv('JAVA_HOME')
                cppCompiler.args "-I$javaHome/include"
                cppCompiler.args "-I$javaHome/include/darwin"
            } else if (targetPlatform.operatingSystem.isLinux()) {
                def javaHome = System.getenv('JAVA_HOME')
                cppCompiler.args "-I$javaHome/include"
                cppCompiler.args "-I$javaHome/include/linux"
            }
        }
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"
            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
    }
}

static def findGoogleTestStaticLib(Platform p) {
    if (p.operatingSystem.windows) {
        return "Release/gtest.lib"
    } else {
        return "libgtest.a"
    }
}

jacoco {
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

javadoc {
    options.charSet("utf-8")
    options.encoding("utf-8")
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "halu5071"
            name "Yasunori Horii"
            email "horiiortho5@gmail.com"
        }
    }

    scm {
        url "https://github.com/halu5071/edwards"
    }
}

publishing {
    publications {
        EdwardsPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId 'io.moatwel.crypto'
            artifactId 'eddsa'
            version '0.2.0-alpha'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.archivesBaseName)
                root.appendNode('url', 'https://github.com/halu5071/edwards')
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER_NAME')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['EdwardsPublication']
    override = true

    pkg {
        repo = 'edwards'
        name = 'edwards'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/halu5071/edwards'
        vcsUrl = 'https://github.com/halu5071/edwards.git'
        version {
            name = project.version
            desc = project.description
            released = new Date()
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_GPG_PASSWORD')
            }
        }
    }
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

